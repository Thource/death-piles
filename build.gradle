plugins {
	id 'java'
	id "com.diffplug.spotless" version "6.25.0"
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
	compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.20'

	implementation('com.google.api-client:google-api-client:2.6.0') {
		exclude group: 'com.google.oauth-client'
		exclude group: 'com.google.guava'
		exclude group: 'org.apache.httpcomponents'
		exclude group: 'commons-codec'
	}
	implementation('com.google.oauth-client:google-oauth-client-jetty:1.36.0') {
		exclude group: 'com.google.guava'
		exclude group: 'org.apache.httpcomponents'
	}
	implementation('com.google.apis:google-api-services-sheets:v4-rev20240514-2.0.0') {
		exclude group: 'com.google.guava'
		exclude group: 'org.apache.httpcomponents'
		exclude group: 'commons-logging'
		exclude group: 'commons-codec'
	}

	annotationProcessor 'org.projectlombok:lombok:1.18.20'

	testImplementation 'junit:junit:4.13.1'
	testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion
}

group = 'dev.thource.runelite.dudewheresmystuff'
version = '2.5.0'
//noinspection GroovyUnusedAssignment
sourceCompatibility = '1.8'

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
	//ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '.gitattributes', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		// don't need to set target, it is inferred from java

		// apply a specific flavor of google-java-format
		googleJavaFormat('1.8').reflowLongStrings()//.skipJavadocFormatting()
		// fix formatting of type annotations
		formatAnnotations()
		// make sure every file has the following copyright header.
		// optionally, Spotless can set copyright years by digging
		// through git history (see "license" section below)
//        licenseHeader '/* (C)$YEAR */'
	}
}
